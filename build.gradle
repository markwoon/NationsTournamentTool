plugins {
  id 'java'
  id 'application'
  // javafx plugin must come before jlink plugin
  id 'org.openjfx.javafxplugin' version '0.0.12'
  id 'org.beryx.jlink' version '2.24.4'
  id 'org.gradlex.extra-java-module-info' version '1.4.1'
  id 'org.javamodularity.moduleplugin' version '1.8.10'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

version = project.findProperty('version')
group = 'org.markwoon'



repositories {
  mavenCentral()
}

dependencies {
  implementation group: 'com.sun.mail', name: 'jakarta.mail', version: '2.0.1'
  implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
  implementation group: 'org.apache.commons', name: 'commons-text', version: '1.10.0'
  implementation group: 'org.apache.poi', name: 'poi', version: '5.2.3'
  implementation group: 'org.apache.poi', name: 'poi-ooxml', version: '5.2.3'
  implementation group: 'org.apache.poi', name: 'poi-ooxml-lite', version: '5.2.3'
  implementation group: 'com.google.guava', name: 'guava', version: '32.0.1-jre'
  implementation group: 'org.checkerframework', name: 'checker-qual', version: '3.35.0'
  implementation group: 'org.jsoup', name: 'jsoup', version: '1.16.1'
  implementation group: 'org.slf4j', name: 'slf4j-nop', version: '1.7.36'

  testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.3'
  testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.9.3'
}


// this is necessary to add Java module info to non-module libraries
// reference: https://stackoverflow.com/a/74003929/1063501
extraJavaModuleInfo {
  failOnMissingModuleInfo.set(false)
  automaticModule('commons-math3-3.6.1.jar', 'commons.math3')
  automaticModule('SparseBitSet-1.2.jar', 'SparseBitSet')
}


javafx {
  version = "17"
  modules = [ 'javafx.controls', 'javafx.fxml']
}

application {
  mainModule    = 'org.markwoon.nations'
  mainClass.set('org.markwoon.nations.TournamentTool')
}

test {
  useJUnitPlatform()
  testLogging {
    // set options for log level LIFECYCLE
    events 'passed', 'skipped', 'failed'
    exceptionFormat 'short'

    // remove standard output/error logging from --info builds
    // by assigning only 'failed' and 'skipped' events
    info.events = ['failed', 'skipped']

    // set options for log level DEBUG
    debug.events = ['started', 'passed', 'skipped', 'failed']
    debug.exceptionFormat = 'full'
  }
}

jlink {
  options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
  launcher {
    name = 'TournamentTool'
    noConsole = true
  }
  jpackage {
    appVersion = version

    if (org.gradle.internal.os.OperatingSystem.current().windows) {
      installerType = 'msi'
      imageOptions = ['--icon', 'src/main/assets/marie_curie.ico']
      installerOptions = ['--win-menu', '--win-shortcut', '--win-upgrade-uuid', UUID.randomUUID().toString()]
    }
    if (org.gradle.internal.os.OperatingSystem.current().macOsX) {
      installerType = 'pkg'
      imageOptions = ['--icon', 'src/main/assets/marie_curie.icns']
    }
  }
}
